plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'org.inferred.processors' version '3.7.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'com.devshawn'

mainClassName = 'com.devshawn.kafka.gitops.MainCommand'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

dependencies {
    implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '3.2.3'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.4'
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.4"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.13.4"
    implementation 'info.picocli:picocli:4.6.3'

    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.1'
    implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'

    annotationProcessor 'org.inferred:freebuilder:2.7.0'
    compileOnly 'org.inferred:freebuilder:2.7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testCompileOnly group: 'junit', name: 'junit', version: '4.13'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    testImplementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.5'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.2-groovy-4.0'
    testImplementation group: 'org.spockframework', name:'spock-junit4', version: '2.2-groovy-4.0'
    testImplementation group: 'cglib', name: 'cglib-nodep', version: '2.2'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testImplementation group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.1'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = true
        html.enabled = true
        xml.outputLocation.set(file("${buildDir}/reports/jacoco/report.xml"))
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    excludes: [
                            '**/*_Builder*/**',
                    ])
        }))
    }
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': 'com.devshawn.kafka.gitops.MainCommand'
        )
    }
}

task buildExecutableJar(type: Exec) {
    dependsOn shadowJar
    commandLine "sh", "build.sh"
}

task buildRelease(type: Zip, group: "build") {
    dependsOn buildExecutableJar
    from("$buildDir/output")
}
